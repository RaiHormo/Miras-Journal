~ getting_dark
...It's getting a little dark.
=> END

~ that_should_do_it
That should do it.
=> END

~ was_that_a
It... ran away?[wait=0.4]\nWas that another monster?
Maybe I should follow it.
=> END

~ cant_dash_on_flame
do portrait("MiraNeutural")
Mira: I can't dash and hold this flame at the same time.[next=4]
=> END

~ hey_wait
do portrait("MiraOpenMouth")
Mira: Hey wait![next=2]
=> END

~ approach
It seems scared of me...
=> END

~ im_not_gonna_harm_you
do portrait("MiraSmile")
Mira: Come on, it's all fine. I don't want to harm you.
=> END

~ as_lost_as_you
do portrait("MiraNeutural")
Mira: I guess you're just as lost.
do portrait("MiraOpenMouth", false)
Mira: This forest is not safe, we should look for a way out.
=> END

~ haha
do portrait("MiraSmile")
Mira: Haha, you can have that if you want!
do portrait("MiraSmile")
Mira: Here.
=> END

~ good_on_you
do portrait("MiraSmile")
Mira: Looks good on you!
=> END

~ stay_back
do portrait("MiraOpenMouth")
Mira: You stay behind me,\nI'll deal with it!
=> END!

~ skeleton
if not f("LookedAtSkeleton"):
	do f("LookedAtSkeleton", true)
	...There's no mistaking it, that's a skeleton.
	From the skull shape it seems human, but I don't want to look at it more than I have to.
else: 
	...Let's just move on.
=> END!

~ symbol
if flag_int("LookedAtTempleSymbol") == 0:
	do flag_progress("LookedAtTempleSymbol", 1)
	There is a symbol engraved on the ground.
	Looks like some sort of bird... with a clover head?
	...
	I just noticed that it's not perfectly symmetrical, which makes looking at it increasingly painful.
elif flag_int("LookedAtTempleSymbol") == 1: 
	do flag_progress("LookedAtTempleSymbol", 2)
	The pain grows.
elif flag_int("LookedAtTempleSymbol") == 2:
	do flag_progress("LookedAtTempleSymbol", 3)
	This place seems like an old abandoned temple, so this must be some sort of religious symbol.
	I don't recall ever seeing something like it though.
else: 
	Let's move on, I can't stand here looking at this all day.
=> END

~ statue_legs
Looks like a statue.
Or, to be more precise, the legs of a statue.
From the shape, these seem more like human legs, but it's hard to tell.
=> END

~ not_going_well
do portrait("MiraNeutural")
Mira: Ow, this hurts...
do portrait("MiraNeutural", false)
Mira: Do I have anything that could heal me?
=> END

~ going_nowhere
do portrait("MiraNeutural")
Mira: If this goes on...
do portrait("MiraNeutural", false)
Mira: Hey, I think we might have to run away!
=> END

~ amazing
do portrait("MiraNeutural")
Mira: Woah, you can use healing magic!?
do portrait("MiraNeutural", false)
Mira: We might have a chance like this!
Alcine: (happy noises)
=> END