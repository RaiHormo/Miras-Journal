~ was_that_a
It ran away?[wait=0.4] Was that another monster?
Maybe I should follow it.
=> END

~ first_cutscene
Placeholder description: A cutscenre that isn't made yet should play here.
=> END
Placeholder description: Whenever a box like this appears, pretend you're seeing whatever is described on screen.
Placeholder description: You see a paw walk through the grass.
Placeholder description: The camera pans upwards to reveal a girl with an orange tunic and gray fur walking.
Placeholder description: She stops and looks forward.
Placeholder description: The camera cuts to a view of a forest next to a lake, and a tree in the middle.
Placeholder description: The girl sits under the tree, leaving her bag next to her.
Placeholder description: She opens the bag and takes a piece of paper. Then unfolds it.
Placeholder description: Weird scribbles are on it. She turns it around and there's an empty page.
Placeholder description: She opens a pen case in her bag and takes one pen.
I don't even know what the date is.
Today I woke up in the middle of this strange forest.
I can't recall how I got here or what I'm doing here, my memory is a fog.
Well, maybe that's not completly accurate, I can remember who I am at least.
Mira Levenor, 19 year old Cyrenian who lives in Neortoria.
I have to write that down in case I forget that too.
Placeholder description: The camera pans up to the sky showing it's strange blurry color mix and lines across it.
This place must be pretty far from my home, the sky looks very strange.
Placeholder description: The camera cuts to the lake as Mira tries to take some water.
Mira: (Sigh)
Placeholder description: The water drops slowly from her arm like slime.
Mira: This is definitely not drinkable...
Placeholder description: The camera cuts to mira below the tree hugging her legs
Mira: What can I even do?
Placeholder description: As she sits there a tentacle looking thing reaches towards her.
Mira: GAH!
Placeholder description: Mira grabs the axe from her bag and slices the tentacle, which bursts with slime.
Placeholder description: A monster emerges from the lake, as Mira takes a fighting stance.
=> END

~ sstay_back
Mira.Neutural: S-[wait=0.2]stay back...!
=> END

~ gahh
Mira.Neutural: GAHH![wait=1] GET OFF ME!
=> END

~ my_aura
Mira.Neutural: These wings...
Mira.Neutural: I can use [outline_size=12][outline_color=#6c3e18][color=#ff6b50]Heat magic[/color][/outline_color][/outline_size] against it![next=5]
Mira.Neutural: Ok, I think I can do it.
=> END

~ what_this
There might be more of these monster up ahead, I should be careful...
do get_node("/root/Battle/Act/Actor0").hide()
set Global.Player.position = get_node("/root/Battle/Act/Actor0").global_position
do Global.Player.show()
Hm, what's that?
do Global.Player.move_dir(Vector2(2, 0), true, false)
do Event.wait(0.1)
set Global.Player.BodyState = NPC.CUSTOM
#do Global.Player.set_anim("PickUpRight", true, true)
do Item.add_item("WhiteSlime", "Mat")
do Event.wait(1)
This slime seems to contain some of that monster's essence.
do Global.Player.set_anim("IdleRight")
set get_node("/root/Battle/Act/Actor0").global_position = Global.Player.position
I feel like it might prove useful.
=> END

~ hey_wait
Mira.OpenMouth: Hey wait![next=2]
=> END

~ approach
It seems scared...
Maybe I should try approaching it.
=> END

~ you_ok
Mira.Neutural: Hey.
=> END

~ no_harm
Mira.Smile: Don't worry, I mean no harm. 
Mira.Smile: You just seem scared, and...
=> END

~ haha
do portrait("AlcineUpset")
do next_box("Alcine")
Shewww
The creature tightly hugs me.
Their body feels like it's made of liquid...
Mira.Neutural: What are you so scared about?
do npc("Alcine").move_dir(Vector2(1,0))
do npc("Alcine").look_to(Vector2(-1,0))
Mira.Smile: Everything will be ok!
do portrait("AlcineHappy")
do next_box("Alcine")
Whimm
do Global.Player.get_node("%Flame").play("ReachOut2")
Mira.Smile: Here, you can have this.
I take a green fabric from my bag, and tie it around their neck.
=> END

~ good_on_you
Mira.Smile: Looks good on you!
=> END

~ stay_back
Mira.Neutural: Is this what scared you?
Mira.Neutural: Stay behind me, I'll deal with it!
=> END!

~ not_going_well
Mira.Neutural: Ow, this hurts...
Mira.Neutural: Is there anything I can do?
=> END

~ going_nowhere
Mira.Neutural: If this goes on...
=> END

~ amazing
Mira.Neutural: Woah, you can use healing magic!?
Mira.Neutural: We might have a chance like this!
Alcine.Happy: /*happy noises*/
=> END

~ got_through_that
Mira.Neutural: We should be safe now.
Mira.Neutural: If it wasn't for you, I'd be done for.
Mira.Neutural: We should stick together for now, [wait=0.5]umm...
Mira.Neutural: Wait, do you have a name?
Mira.Neutural: (hide)[next]
do bubble("Question", "Alcine")
[wait=1] [next=auto]
Mira.Neutural: Right, you can't talk. But I need to call you something...
Mira.Neutural: Ok, I'll come up with a name and you tell me if you like it. Alright?

~ what_about
[wait=0.1] [next=auto]
do Global.Area.get_node("AlcineNaming").start()
if alcine() == "Alcine":
	"Alcine". I think that's the best name I can think of.
elif alcine() == "Pikachu":
	Pika pika?
elif alcine() == "Raichu":
	Rai?
elif alcine() == "Watery":
	Very straight forward.
elif alcine() == "Mario":
	I maybe I should name them Sonic instead.
elif alcine() == "Sonic":
	I'd rather just name them Mario.
elif alcine() == "Sans":
	(deep breath)[wait=1] SA-[wait=0.1][next]
	Shall I use this name?
elif alcine() == "Journal":
	And then she said "It's journaling time!" and journaled all over them.
elif alcine() == "Name":
	Name?
elif alcine() == "Gaster":
	I don't speak hands.
elif alcine() == "Luigi":
	Respectable.
elif alcine() == "Rai" or alcine() == "Raidev":
	SECRET DEVELOPER MODE ACTIVATED! Or something...
elif alcine() == "Iris":
	Truly.
elif alcine() == "Petrogon":
	That's quite ironic.
elif alcine() == "Godot":
	The very fabric of this world.
elif alcine() == "Kirby":
	Poyo!
elif alcine() == "Dog":
	Uhhhh, sure...
elif alcine() == "Cat" or alcine() == "Kitty":
	Is that really fitting?
elif alcine() == "Rat":
	Squeak!
elif alcine() == "Alcin":
	Silent letters are for loosers.
elif alcine() == "Chip":
	That's the way it is.
elif alcine() == "Harold":
	har har, very funny
elif alcine() == "Bob":
	The true name?
elif alcine() == "Alcinoe":
	A godly name.
elif alcine() == "Alci":
	Short and sweet.
elif alcine() == "Alc":
	Maybe too short?
elif alcine() == "Al":
	Weird.
elif alcine() == "Alcaine":
	No, too far.
elif alcine() == "Alkaline":
	It's pronounced "Alk-ah-leen".
elif alcine() == "Cone" or alcine() == "Alcone" or alcine() == "Alex":
	Brazy.
elif alcine() == "Creature":
	Bit on the nose?
else:
	"{{alcine()}}". Should I go with this?
- Use this name
	do Area.get_node("AlcineNaming").hide()
	=> use_name
- Let's rethink this
	=> what_about

~ use_name
Mira.Neutural: What about "{{alcine()}}"?
Alcine.Neutural: /*nod*/
do npc("P").look_to(Vector2(1, 0))
Mira.Neutural: Alright then {{alcine()}}, let's go together from here on.
=> END

~ a_bridge
Mira.Neutural: Oh a bridge.
Mira.Neutural: Seems like this is our way out of this forest.
=> END

~ morning
Mira.Neutural: The sky is getting brighter. [p]Seems like it's early morning.
[wait=0.3] [next=auto]
do npc("MiraCut").look_to(Vector2(0, -1))
[wait=0.1] [next=auto]
do npc("MiraCut").bubble("Surprise")
[wait=0.1] [next=auto]
do npc("AlcineCut").look_to(Vector2(0, -1))
Mira.Neutural: Look {{find_member("Alcine").FirstName}}!
[wait=0.1] [next=auto]
do npc("AlcineCut").chain_moves([Vector2(-1, 0), Vector2(0, -1)])
Mira.Neutural: That looks like a town, doesn't it?
[wait=0.1] [next=auto]
do npc("AlcineCut").bubble("Excited")
Mira.Neutural: Come on let's go! The people there might be able to help us!
=> END

~ what_happened_here
Mira.Neutural: What... happened here?
[wait=0.1] [next=auto]
do npc("P").go_to(Vector2(6, 15))
do npc("P").look_to(Vector2(1, 0))
[wait=0.1] [next=auto]
do npc("P").look_to(Vector2(-1, 0))
[wait=0.1] [next=auto]
do npc("P").look_to(Vector2(0, -1))
Mira.Neutural: It's all... in ruins...
do take_control()
[wait=0.1] [next=auto]
do npc("P").chain_moves([Vector2(-1, 0), Vector2(0, -1)])
[wait=0.1] [next=auto]
do npc("P").camera_follow(false)
do npc("P").BodyState = NPC.CUSTOM
do npc("P").set_anim("SitRight")
do npc("P").collision(false)
do jump_to(Player, Vector2(107, 345), 15)
Mira.Neutural: ...
Mira.Neutural: Let's just rest here for a bit, I'm exhausted.
Alcine.Tired: /*{{alcine()}} looks tired as well*/
=> END

~ rest_amberelm
Placeholder description: This is an unfinished cutscene. Boxes like this will describe what should be on screen.
Placeholder description: Mira is writing something on a paper.
Mira.Neutural: Hm? Something up {{alcine()}}?
Mira.Neutural: I'm just keeping track where we've been so far.
Mira.Neutural: We don't want to get even more lost, right?
Mira.Neutural: (Yaaawn...)[next=3]
Mira.Neutural: Must be nice being a spirit, I'm starving here.
Placeholder description: Mira takes a nap.
Alcine.Neutural: ...
Placeholder description: {{alcine()}} picks up the paper.
=> END

~ wake_amberelm
Placeholder description: Mira gets up from sleep.
Placeholder description: {{alcine()}} who was resting on Mira also gets up.
Mira.Neutural: Mmm... Good morning {{alcine()}}. Or is it afternoon?
Alcine.Tired: Meuummm...
Placeholder description: Mira picks up the paper from yesterday.
do npc("MiraCut2").bubble("Surprise")
Mira.Neutural: Huh? Is that me?
Alcine.Happy: Mloop!
Mira.Smile: You drew this? That's so cute!
I guess there is one way we can communicate.
Maybe I could get important information from {{alcine()}} this way.
Mira.Neutural: So... I was curious. [p]Before that creature attacked you, how did you end up there?
Mira.Neutural: Do you remember anything?
Alcine.Neutural: /*Thinking noises*/
Placeholder description: {{alcine()}} starts drawing.
Placeholder description: {{alcine()}} gives the drawing to Mira.
Let's see...
Mira.Neutural: Wha- Im being attacked in this?
Alcine.Neutural: /*Nod*/
Mira.Neutural: So you've seen me before.
Mira.Neutural: Weird, I don't remember something like that at all...
Mira.Neutural: Then ended up in that forest? [p]Did you escape the castle?
Alcine.Neutural: /*Head shake*/
Mira.Neutural: Hmm... Do you know how you went from the castle to here? [p]At all?
Alcine.Neutural: /*Head shake*/
Mira.Neutural: Figures.
Mira.Neutural: Well we should probably explore this place a little more before it's too dark.
Mira.Smile: Let's keep on the lookout for any castles around, okay?
=> END

~ kick_bridge
No reason to go back there.
=> END

~ nov3_morning
...
I was seriously hoping it was all a long dream, but it seems this is my home for the time being...

Mira.Neutural: Who is it?

Mira.Neutural: ...
Daze.Neutural: ...
There's a tense atmosphere in the room.

Daze.Neutural: How are you doing?
Mira.Neutural: I'm... okay.
Daze.Neutural: You don't look okay to me...
Mira.Neutural: ...
Mira.Neutural: How can you stay so calm!? We're both stuck here you know!
Mira.Neutural: Am I supposed to just accept that I'll spend the rest of my life in this Donom of a place!?
Mira.Neutural: That everything I've done has been for nothing? That I'll never see my family again?
Daze.Neutural: I didn't...
Mira.Neutural: Do you have nothing going on in your life?! Or are you just pretending to be calm because you need to always look cool?!
Daze.Neutural: Levenor, listen. I didn't mean to blame you.
Daze.Neutural: It's completely understandable for you to feel this way.
Daze.Neutural: As for me... Let's say I've dealt with worse situations.
Daze.Neutural: Both you and I, and everyone here, we all share a goal.
Mira.Neutural: If it was that simple to exit the realm, this whole village wouldn't exist in the first place!
Daze.Neutural: What if I told you I already have a lead?
!
Daze.Neutural: Let's talk about this later in the tavern. I'll be waiting there.
=> END

~ nov3_afternoon
I guess it's time to leave for our "grand journey".
Let's make sure I have everything I need before heading to the gate.
=> END

~ nov3_exit
Alcine.Neutural: Wia!
Asteria.Neutural: What took you so long!
Daze.Neutural: Is everyone ready?
- Just a minute
	Mira.Neutural: Just a minute, I need to do something.
	Asteria.Neutural: Alright, hurry it up!
	do wait(0.2)
	do give_control()
- Let's go
	Mira.Neutural: I'm ready, let's go!
	Daze.Neutural: Someone's excited...
	Asteria.Neutural: I'll lead the way, just follow me!
	do! sequence("nov3_enterSG")
=> END

~ nov3_sg_enter
Asteria.Neutural: Here we are. The place should be somewhere up ahead.
Mira.Neutural: Whoah, everything's so shiny!
Asteria.Neutural: Don't go near the shiny stuff! Those tiny crystal shards hurt!
=> END

~ very_shiny
Daze.Neutural: Levenor, don't you think you of all people should be the most careful?
Mira.Neutural: Huh?
Asteria.Neutural: You're the only one not wearing shoes, haha.
Mira.Neutural: {{alcine()}} isn't wearing shoes either!
Alcine.Tired: Wooom...
=> END

~ cast_lines_sg
do npc("P").look_to("U")
do Player.camera_follow(false)
set Camera.position = Vector2(-158, -18)
do wait(1)
Mira.Neutural: Look, there are a lot of cast lines scattered around here.
Mira.Neutural: Is this some sort of spell?
Asteria.Neutural: This place is a magical organism. This might be just another part of it.
Mira.Neutural: The entire cave is an organism..?
Daze.Neutural: I suspect it's more than just this cave.
Daze.Neutural: The monsters everywhere, they must be a defense mechanism.
Asteria.Neutural: Right, I don't think we're wanted here...
Alcine.Upset: ...
Alcine seems to be bothered by something.
=> END

~ blocked_cast_lines
if not f("EvSwitchCastLines2"):
	Alcine.Neutural: Shaooo!
	Daze.Neutural: These lines gather towards the center.
	Asteria.Neutural: Doesn't this look like the thing that broke the crystals last time?
	- Try touching
		(hide)
		do Player.camera_follow(false)
		do Player.collision(false)
		do Player.go_to(Vector2(-25, -17),true, false)
		do! white_fadeout(0.5, 0, 0, 0.5)
		do! screen_shake()
		set Player.BodyState = NPC.CUSTOM
		do Player.bump(Vector2(0, -2))
		do wait(0.5)
		Asteria.Neutural: Mira! You okay?
		Mira.Neutural: Ow.
		Mira.Neutural: I think there is a protective spell on these.
	- Back off
		Mira.Neutural: Everyone, be careful!
		Mira.Neutural: It looks like there is some sort of protective spell, don't touch that.
	Daze.Neutural: What could we do about this? Does anyone know how to dispell? 
	Mira.Neutural: Wild cast patterns like this are hard to decipher.
	Asteria.Neutural: ...
	Asteria.Neutural: I think there's someone back at the village that would know how.
	Mira.Neutural: You mean we should return?
	Daze.Neutural: I think we could all use some rest, no?
	do Player.camera_follow(true)
	Asteria.Neutural: There was a Vain nearby, let's head there. 
	do add_flag("EvSwitchCastLines2")
else:
	Asteria.Neutural: Mira! We can't make any progress here unless we despell this.
	Mira.Neutural: I know, I know.
	Asteria.Neutural: Then use the Vain to return to the village already!
=> END