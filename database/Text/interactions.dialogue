~ getting_dark
...It's getting a little dark.
=> END

~ that_should_do_it
This should help.
=> END

~ cant_dash_on_flame
Mira.Neutural: I can't dash and hold this flame at the same time.[next=4]
=> END

~ skeleton
if not f("LookedAtSkeleton"):
	do f("LookedAtSkeleton", true)
	...There's no mistaking it, that's a skeleton.
	From the skull shape it seems human, I think.	
else: 
	...Let's just move on.
=> END!

~ symbol
if flag_int("LookedAtTempleSymbol") == 0:
	do flag_progress("LookedAtTempleSymbol", 1)
	There is a symbol engraved on the ground.
	Looks like some sort of bird... with a clover head?
	...
	I just noticed that it's not perfectly symmetrical, which makes looking at it increasingly painful.
elif flag_int("LookedAtTempleSymbol") == 1: 
	do flag_progress("LookedAtTempleSymbol", 2)
	The pain grows.
elif flag_int("LookedAtTempleSymbol") == 2:
	do flag_progress("LookedAtTempleSymbol", 3)
	This place seems like an old abandoned temple, so this must be some sort of religious symbol.
	I don't recall ever seeing something like it though.
else: 
	Let's move on, I can't stand here looking at this all day.
=> END

~ statue_legs
if not f("LookedAtStatue"):
	do f("LookedAtStatue", true)
	Looks like a statue.
	Or, to be more precise, the legs of a statue.
else:
	do f("LookedAtStatue", false)
	I can't tell the species of whoever this satue depicted.
	But it's probably not Cyrenian or Eyvi judging from the shape.

=> END

~ amberelm_sign
It's a broken sign with Cyrenian writing on it
"Amberelm town"
=> END

~ broken_bridge
This bridge is broken. \nWe should find another way to get there.
=> END

~ day10_gap
if f("RestAmberelm"):
	These wires seem tough.
	I should be able to get to the other side with enough speed.
else:
	Wonder if I get to the other side using these wires.
	But I'm quite tired right now, let's rest first.
=> END

~ the_sky
do Global.Player.camera_follow(false)
do Global.Player.look_to(Vector2(0, -1))
set Global.get_cam().position -= Vector2(0, 24)
do Event.wait(0.5)
if Event.f("TheSkyAmberelm"):
	Mira.Neutural: ...
else:
	Mira.Neutural: The sky here is as strange as ever.
	Mira.Neutural: Looks like we're near the sea, but it fades into those strange lines.
	Mira.Neutural: It's definitely far from my home...
	do Event.f("TheSkyAmberelm", true)
=> END

~ wrong_way
 Mira.Neutural: [[I thing we're going the wrong way.|I dont this this is where we should be going...|There isn't anything to see back here, let's go the other way.]]
=> END

~ just_woke_up
I just woke up. Don't feel like spending all day in bed.