~ amberelm_sign
It's a broken sign with Cyrenian writing on it
"Amberelm town"
=> END

~ the_sky
do Global.Player.camera_follow(false)
do Global.Player.look_to(Vector2(0, -1))
set Global.get_cam().position -= Vector2(0, 24)
do Event.wait(0.5)
if Event.f("TheSkyAmberelm"):
	Mira.Neutural: ...
else:
	Mira.Neutural: The sky here is as strange as ever.
	Mira.Neutural: Looks like we're near the sea, but it fades into those strange lines.
	Mira.Neutural: It's definitely far from my home...
	do Event.f("TheSkyAmberelm", true)
=> END

~ morning
Mira.Neutural: The sky is getting brighter. \nSeems like it's early morning.
[wait=0.3] [next=auto]
do npc("MiraCut").look_to(Vector2(0, -1))
[wait=0.1] [next=auto]
do npc("MiraCut").bubble("Surprise")
[wait=0.1] [next=auto]
do npc("AlcineCut").look_to(Vector2(0, -1))
Mira.Neutural: Look {{find_member("Alcine").FirstName}}!
[wait=0.1] [next=auto]
do npc("AlcineCut").chain_moves([Vector2(-1, 0), Vector2(0, -1)])
Mira.Neutural: That looks like a town, doesn't it?
[wait=0.1] [next=auto]
do npc("AlcineCut").bubble("Excited")
Mira.Neutural: Come on let's go! The people there might be able to help us!
=> END

~ what_happened_here
Mira.Neutural: What... happened here?
[wait=0.1] [next=auto]
do npc("P").go_to(Vector2(6, 15))
do npc("P").look_to(Vector2(1, 0))
[wait=0.1] [next=auto]
do npc("P").look_to(Vector2(-1, 0))
[wait=0.1] [next=auto]
do npc("P").look_to(Vector2(0, -1))
Mira.Neutural: It's all... in ruins...
do take_control()
[wait=0.1] [next=auto]
do npc("P").chain_moves([Vector2(-1, 0), Vector2(0, -1)])
[wait=0.1] [next=auto]
do npc("P").camera_follow(false)
do npc("P").BodyState = NPC.CUSTOM
do npc("P").set_anim("SitRight")
do npc("P").collision(false)
do jump_to(Player, Vector2(107, 345), 15)
do Area.Followers[0].dont_follow = false
Mira.Neutural: ...
Mira.Neutural: Let's just rest here for a bit, I'm exhausted.
Alcine.Tired: /*{{alcine()}} looks tired as well*/
=> END

~ broken_bridge
This bridge is broken. \nWe should find another way to get there.
=> END

~ day10_gap
if f("RestAmberelm"):
	These wires seem tough.
	I should be able to get to the other side with enough speed.
else:
	Wonder if I get to the other side using these wires.
	But I'm quite tired right now, let's rest first.
=> END

~ rest_amberelm
Placeholder description: This is an unfinished cutscene. Boxes like this will describe what should be on screen.
Placeholder description: Mira is writing something on a paper.
Mira.Neutural: Hm? Something up {{alcine()}}?
Mira.Neutural: I'm just keeping track where we've been so far.
Mira.Neutural: We don't want to get even more lost, right?
Mira.Neutural: (Yaaawn...)[next=3]
Mira.Neutural: Must be nice being a spirit, I'm starving here.
Placeholder description: Mira takes a nap.
Alcine.Neutural: ...
Placeholder description: {{alcine()}} picks up the paper.
=> END

~ wake_amberelm
Placeholder description: Mira gets up from sleep.
Placeholder description: {{alcine()}} who was resting on Mira also gets up.
Mira.Neutural: Mmm... Good morning {{alcine()}}. Or is it afternoon?
Alcine.Tired: Meuummm...
Placeholder description: Mira picks up the paper from yesterday.
do npc("MiraCut").bubble("Surprise")
Mira.Neutural: Huh? Is that me?
Alcine.Happy: Bloop!
Mira.Smile: You drew this? That's so cute!
I guess there is one way we can communicate.
{{alcine()}} seems a lot smarter than I thought. Maybe I should ask some more important questions.
Mira.Neutural: So... I was curious. \nBefore that monster found you, how did you end up here?
Mira.Neutural: Do you remember anything?
Alcine.Neutural: /*Thinking noises*/
Placeholder description: {{alcine()}} starts drawing.
Placeholder description: {{alcine()}} gives the drawing to Mira.
Let's see...
Mira.Neutural: Wha- Im being attacked in this?
Alcine.Neutural: /*Nod*/
Mira.Neutural: So you've seen me before.
Mira.Neutural: Weird, I don't remember something like that at all...
Mira.Neutural: So you were imprisoned in some kind of... castle?
Mira.Neutural: Then ended up in that forest? \nDid you escape the castle?
Alcine.Neutural: /*Head shake*/
Mira.Neutural: Hmm... Do you know how you went from the castle to here? \nAt all?
Alcine.Neutural: /*Head shake*/
Mira.Neutural: Figures.
Mira.Neutural: Well we should probably explore this place a little more before it's too dark.
Mira.Smile: Let's keep on the lookout for any castles around, okay?
=> END

~ kick_bridge
No reason to go back there.
=> END